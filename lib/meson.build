flags = '-Wl,--version-script=' + meson.current_source_dir() + '/libjose.map'
code = 'int main() { return 0; }'
cc = meson.get_compiler('c')

if not cc.links(code, args: flags, name: '-Wl,--version-script=...')
  flags = [ '-export-symbols-regex=^jose_.*' ]
endif

libjose = library('jose',
  sources: [
  'misc.c',
  'cfg.c',
  'io.c',
  'b64.c',
  'hsh.c',
  'hooks.c',
  'jwk.c',
  'jws.c',
  'jwe.c',
  'zlib/deflate.c',
  'openssl/aescbch.c',
  'openssl/aesgcm.c',
  'openssl/aesgcmkw.c',
  'openssl/aeskw.c',
  'openssl/compat.c',
  'openssl/dir.c',
  'openssl/ec.c',
  'openssl/ecdh.c',
  'openssl/ecdhes.c',
  'openssl/ecmr.c',
  'openssl/ecdsa.c',
  'openssl/hash.c',
  'openssl/hmac.c',
  'openssl/jwk.c',
  'openssl/lock.c',
  'openssl/misc.c',
  'openssl/oct.c',
  'openssl/pbes2.c',
  'openssl/rsa.c',
  'openssl/rsaes.c',
  'openssl/rsassa.c',
  ],
  include_directories: libjose_inc,
  dependencies: [zlib, jansson, libcrypto],
  version: '0.0.0',
  link_args: flags,
  install: true
)

